
plugins {
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
    id 'com.diffplug.spotless'
}

repositories {
    gradlePluginPortal()
    mavenCentral()
}

group = 'net.vino9.vino'
version = '1.0.7-SNAPSHOT'
java.sourceCompatibility = JavaVersion.VERSION_17

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withSourcesJar()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'

    // Enable the -Xlint:unchecked option
    options.compilerArgs << "-Xlint:unchecked"
}

tasks.withType(Javadoc) {
    options.encoding = 'utf-8'
}

jacoco {
    toolVersion = "0.8.11"
}

jacocoTestReport {
    dependsOn test
}

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport

    testLogging {
        showStandardStreams = System.getenv("CI") == "true"
    }
}


spotless {
    // ratchetFrom 'origin/develop'
    enforceCheck true

    format 'misc', {
        // define the files to apply `misc` to
        target '*.gradle', '.gitattributes', '.gitignore'

        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()
    }

    java {
        targetExclude 'build/**/*.*'
        toggleOffOn()
        importOrder()
        removeUnusedImports()
        googleJavaFormat('1.17.0').aosp().reflowLongStrings().skipJavadocFormatting()
        formatAnnotations()
    }
}
